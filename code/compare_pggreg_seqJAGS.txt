model {

### PRIORS ----------------------------
# variance for coefficients
sigma_sq0 ~ dgamma(alpha, beta)
for(index in 1:(npred_pgg)){
  sigma_sq[index] ~ dgamma(alpha, beta)
}

#thresholds/intercept
pgg_int1 ~ dnorm(0,0.01)
pgg_int2 ~ dnorm(pgg_int1, 1/sigma_sq0)
pgg_int3 ~ dnorm(pgg_int2, 1/sigma_sq0)

#sequential glm coefficients (other than intercept)
for(index in 1:(npred_pgg-(nlevel_cancer-1))) {
  pgg_slope1[index] ~ dnorm(0,0.01)
  pgg_slope2[index] ~ dnorm(pgg_slope1[index],1/sigma_sq[index])
  pgg_slope3[index] ~ dnorm(pgg_slope2[index],1/sigma_sq[index])
}
#truncate step function's coefs at -1
for (index in (npred_pgg-(nlevel_cancer-1)+1):(npred_pgg)) {
  pgg_slope1[index] ~ dnorm(0, 0.01)T(-1,) 
  pgg_slope2[index] ~ dnorm(pgg_slope1[index], 1/sigma_sq[index])T(-1,) 
  pgg_slope3[index] ~ dnorm(pgg_slope2[index], 1/sigma_sq[index])T(-1,) 
}


### LIKELIHOOD ----------------------------
for(j in 1:npat_pgg1) {
	lin.pred1[j] <- inprod(pgg_slope1[1:npred_pgg], modmat_pgg1[j,1:npred_pgg]) 
	logit(p_rc[j, 1]) <- pgg_int1 + lin.pred1[j]
	pgg_data1[j] ~ dbern(p_rc[j, 1]) 
}

for(j in 1:npat_pgg2) {
	lin.pred2[j] <- inprod(pgg_slope2[1:npred_pgg], modmat_pgg2[j,1:npred_pgg]) 
	logit(p_rc[j, 2]) <- pgg_int2 + lin.pred2[j]
	pgg_data2[j] ~ dbern(p_rc[j, 2]) 
}

for(j in 1:npat_pgg3) {
	lin.pred3[j] <- inprod(pgg_slope3[1:npred_pgg], modmat_pgg3[j,1:npred_pgg]) 
	logit(p_rc[j, 3]) <- pgg_int3 + lin.pred3[j]
	pgg_data3[j] ~ dbern(p_rc[j, 3]) 
}

}
